# Update - Our Logic

The `update` function is how are application is going to response to the actions and messages being passed around by our user interactions. Most of our business logic will live inside the `update` function.

The `update` function takes two arguments: the msg, and the current state of the model. The it returns a new model. The type signature looks like this:


```
update : Msg -> Model -> Model
```

English:

1. `update` has type of `Msg` to `Model` to `Model`
2. `update` takes two arguments of type `Msg` and of type `Model` and returns a new value that is a `Model`

Lets implement a bar bones `update` function:

```
update : Msg -> Model -> Model
update msg model =
  model
```

Normally, I put this function under my `view` function. So, our full code looks like:

```
import Html exposing (..)
import Html.Events exposing (onInput, onClick)


main =
  view init

type alias Model =
  { todos : List String
  }

type Msg
  = OnUserInput String
  | OnAddToDo

init : Model
init =
  { todos = [ "Add some todos" ]
  }

view : Model -> Html Msg
view model =
  div
    []
    ([ h1
        []
        [ text "Todo List" ]
      , renderUserInputArea model
      ]
        ++ (List.map renderTodo model.todos))

update : Msg -> Model -> Model
update msg model =
  model



renderTodo : String -> Html Msg
renderTodo todo =
  p
    []
    [ text todo ]


renderUserInputArea : Model -> Html Msg
renderUserInputArea model =
  div
    []
    [ input
        [ onInput OnUserInput
        ]
        []
    , button
        [ onClick OnAddToDo
        ]
        [ text "Add Todo!" ]
    ]
```
Right now our update function is lacking for a few reasons. One it is not "listening" to the `view` for when the user sends our application messages, and two it literally does not do anything. We will cover both these issues now:


## Program

Elm Architecture is both a model, view, and update (MVU) architecture, it also involves another package that does all the hooking up of our model, view, and update functions for us. That package is called `Html.App`. There a few function in this package, but the one we are going to look is `beginnerProgram`. This is for programs that are not going to be talking to the outside world at all, just like our current one. Later when we build our bigger project we will use the `program` function that will enable us to talk to JavaScript and make network requests. We are going to import that package now into our app using a new import syntax called the `as` import. Here is the import statement:

```
import Html.App as App

```

Lets generalize this import statement: `import PackageName as SomeOtherName`.

The `SomeOhterName` can be anything, but it is good practice for it to be somewhat descriptive to the real package name. In away you are just aliasing the package name. Normally the alias is either the first letter of the package name or if your package has a dot like `Html.App` just the first letters of each name so like `HA` in our case. However, for `Html.App` I have come to prefer just `App` because that is clear to me that application level stuff will be produced by using functions in this package.


### `beginnerProgram`

The type signature for the `beginnerProgram` from our `App` package looks like:


```
beginnerProgram : { model : model, view : model -> Html msg, update : msg -> model -> model } -> Program Never
```

Woh! Basically it says: `beginnerProgram` has type of record to `Program Never`

The records here can be rewriting has an alias to help see it a little better:

```

type alias BeginnerProgramRecord =
  { model : model
  , view : model -> Html msg
  , update : msg -> model -> model
  }
```

We have three fields with all type variables. The first one all be anything: `Float`, `Int`, `String`, `List`, and the list goes on. The next two have to be functions has we see the `->` in their type signatures. If you take a look at the signatures for the view field we will see that it matches our `view` function type signature, and the same relation can be seen in our `update` function.


`Program Never` because means we will get a `Program` that will `Never` fail. That is nice of Elm to make such promises!


Lets change our `main` function to kick off our `App` be calling the `beginnerProgram` in the main applying to a recored that will container our `model`, `view`, and `update`.



```
main =
  App.beginnerProgram
    { model = init
    , view = view
    , update = update
    }
```

Here is our full program at this time:

```
import Html exposing (..)
import Html.Events exposing (onInput, onClick)
import Html.App as App

main =
  App.beginnerProgram
    { model = init
    , view = view
    , update = update
    }

type alias Model =
  { todos : List String
  }

type Msg
  = OnUserInput String
  | OnAddToDo

init : Model
init =
  { todos = [ "Add some todos" ]
  }

view : Model -> Html Msg
view model =
  div
    []
    ([ h1
        []
        [ text "Todo List" ]
      , renderUserInputArea model
      ]
        ++ (List.map renderTodo model.todos))

update : Msg -> Model -> Model
update msg model =
  model



renderTodo : String -> Html Msg
renderTodo todo =
  p
    []
    [ text todo ]


renderUserInputArea : Model -> Html Msg
renderUserInputArea model =
  div
    []
    [ input
        [ onInput OnUserInput
        ]
        []
    , button
        [ onClick OnAddToDo
        ]
        [ text "Add Todo!" ]
    ]
```

If you compile this code it will work. This is Elm Architecture at its fullest! It does not seem like anything is happening and that is because our `update` function is not doing anything besides returning a new model that looks just like our old model, thus nothing changes. Lets change that.

## Make It Update

Lets look at our `update` function:

```
update : Msg -> Model -> Model
update msg model =
  model
```

The sole reason for the `update` to exist is to update our model by giving some logical response to the message our user passed in by causing some action in our application. We are given the `msg` and the current `model` a good first step to be able to differentiate between which messages are being sent in. There are two topics that are going to allow us to do that: the `case of` block and something called *pattern matching*.


### `case of`

A `case of` statement can be considered JavaScript's `switch` counter part. The pattern starts like so:


```
case something of
  somecase ->
    dosoemething

  anothercase ->
    dosometing

```

In a way we are passing our case we want to test into the model of the words `case` `of`. Then on the next line, and some white space we can start matching our cases that end in a `->` then new line, and some white space. From here we can write our logic and code that will return us our final value.


### Pattern Matching

This part of Elm may take you a second to appreciate, but once you do and go back to other languages you will miss it greatly. Pattern matching in Elm allows us to match our case statement on different patterns. So lets use our application as an example:


```
update : Msg -> Model -> Model
update msg model =
  case msg of

```

Here is the start. We are going to try to match on our whatever `msg` is passed to us, next we are going to want to pattern match on the messages:


```
update : Msg -> Model -> Model
update msg model =
  case msg of
    OnUserInput value ->
      model

    OnAddToDo ->
      model
```

Still all we are returning is a a new mode of the old model with no updates, which times is fine but we are going to want to update our model. But first, I need I show you the wonderfulness of the Elm compiler. Delete the last two lines of your update function:


```
update : Msg -> Model -> Model
update msg model =
  case msg of
    OnUserInput value ->
      model

```

And now try to compile:

```

-- MISSING PATTERNS ------------------------------------------------------------

This `case` does not have branches for all possibilities.

39|>  case msg of
40|>    OnUserInput value ->
41|>      model

You need to account for the following values:

    Temp1467260063084591.OnAddToDo

Add a branch to cover this pattern!

If you are seeing this error for the first time, check out these hints:
<https://github.com/elm-lang/elm-compiler/blob/0.17.0/hints/missing-patterns.md>
The recommendations about wild card patterns and `Debug.crash` are important!
```

Here is the error message that is basically telling us: "Hey, there are missing a match on a possible type. That type is `OnAddToDo`, so you will need to add that case in order get this to compile." In JavaScript this would fine for years until that one unfortunate day you realize you have been missing user interactions! Another thing to point out is if you did match on all possibilities in JavaScript, but one was missed spelled this information would just slip on by as well.

When Elm (and Haskell) developers say a program is *safe* they are saying once it compiles "it just works" (this is a real saying). They can say this because the compiler is doing more then just type checking, type inferring, compiling to the runtime code, but it even starts to look at our code to make sure we are not doing dumb things. I more recent/common position Elm and Haskell developers are taking it that they need this type of compiler because they are not smart people and make a lot of mistakes in their code. Here is just one example where the compiler saves the day, and saves from use missing potentially useful user actions to our application.

OK, command-z (or control-z) your update function to get it back into this state:

```
update : Msg -> Model -> Model
update msg model =
  case msg of
    OnUserInput value ->
      model

    OnAddToDo ->
      model
```

Lets update our model now. For `OnUserInput` we are saying if the pattern is `OnUserInput someValue` then do the code in the function body. `someValue` is bound to whatever the `target.value` was from the `input` event object. We getting this information from `onInput` what we imported from the `Html.Events` package. For `OnAddToDo` we are matching just on that type, which was triggered when the user clicks on our awesome button. When we have a user input we should update the value of our application, and when the user clicks we should add that current value to our `model.todos` and clear the value.


```
update : Msg -> Model -> Model
update msg model =
  case msg of
    OnUserInput value ->
      { model | value = value }

    OnAddToDo ->
      { model | value = "", todos = List.append model.todos [ model.value ] }
```


Here we are using *record update syntax* from our records primer to update our model and return a new model with the updated fields. This will trigger the virtual dom to render only the parts that are needed to be render, much like React. OK try to compile this code, you should get this:


```
-- TYPE MISMATCH ---------------------------------------------------------------

The type annotation for `update` does not match its definition.

37| update : Msg -> Model -> Model
             ^^^^^^^^^^^^^^^^^^^^^
The type annotation is saying:

    Msg -> { ... } -> { ... }

But I am inferring that the definition has this type:

    Msg -> { b | ..., value : ... } -> { b | ..., value : ... }
```

This `TYPE MISMATCH` error is a little more confusing then he other errors we have experience but what is saying is: "Hey! I noticed the your `Model` in the update function is trying to update a field called `value` but your type signature is saying the `Model` type does not have the field value. You must be smoking something, no compile for you!" The fix is to update our both our `Model` type to have a value field and to add a default value to our `init` function return model:

```
type alias Model =
  { todos : List String
  , value : String
  }

-- some lines of code

init : Model
init =
  { todos = [ "Add some todos" ]
  , value = ""
  }
```
It is good to point out here that Elm is very explicit and thus code tells you everything you need to know by reading through it. At first it might seem annoying but once you get use to this explicitness you will start having to think harder about finding what something is in your code, and more about the problem you are trying to solve. It is quite joyful.

Now it should compile, and you can add some todos! But our input element does not clear. Lets get that fixed! We will need a another package called `Html.Attributes`. In this package we have all our functions for adding Html attributes, like the `value` attribute.

```
import Html exposing (value)
```


Now that we have our `value` function lets use it in our `renderUserInputArea` function!


```
renderUserInputArea : Model -> Html Msg
renderUserInputArea model =
  div
    []
    [ input
        [ onInput OnUserInput
        , value model.value
        ]
        []
    , button
        [ onClick OnAddToDo
        ]
        [ text "Add Todo!" ]
    ]
```

Full working code:

```
import Html exposing (..)
import Html.Events exposing (onInput, onClick)
import Html.App as App
import Html.Attributes exposing (value)


main =
  App.beginnerProgram
    { model = init
    , view = view
    , update = update
    }

type alias Model =
  { todos : List String
  , value : String
  }

type Msg
  = OnUserInput String
  | OnAddToDo

init : Model
init =
  { todos = [ "Add some todos" ]
  , value = ""
  }

view : Model -> Html Msg
view model =
  div
    []
    ([ h1
        []
        [ text "Todo List" ]
      , renderUserInputArea model
      ]
        ++ (List.map renderTodo model.todos))

update : Msg -> Model -> Model
update msg model =
  case msg of
    OnUserInput value ->
      { model | value = value }

    OnAddToDo ->
      { model | value = "", todos = List.append model.todos [ model.value ] }



renderTodo : String -> Html Msg
renderTodo todo =
  p
    []
    [ text todo ]


renderUserInputArea : Model -> Html Msg
renderUserInputArea model =
  div
    []
    [ input
        [ onInput OnUserInput
        , value model.value
        ]
        []
    , button
        [ onClick OnAddToDo
        ]
        [ text "Add Todo!" ]
    ]
```


## Conclusion

This is no way complete, you cannot delete or filter. My challenge to you is to add that functionality on your own. I would also suggest changing the todo item to be a `li` in a `ul` so you can get use to messing around with the `Html` functions. Next we are gong to build a little bigger application from our file system using builds tools, multiple Elm modules, and making HTTP request.


